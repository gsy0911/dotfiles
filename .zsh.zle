
# 1) Prefix 用キーマップを作る
# udk_zsh:= user-defined-kymap zsh
bindkey -N udk_zsh

# 2) Prefix に入るウィジェット
function zle-enter-prefix() {
  zle -K udk_zsh
  zle -M "UDK-ZSH | n:nvim | r: repo | t: task | z: zellij"
}
zle -N zle-enter-prefix

# 3) 「実行したら main に戻す」ための薄いラッパ
function _prefix_wrap() {
  local widget=$1
  shift
  function _inner() {
    zle $widget -- "$@"
    zle -K main
  }
  zle -N "$1" _inner
}
# 4) Prefix 内コマンド（例）
function prefix-left()  { zle backward-char;  zle -K main }
function prefix-down()  { zle down-line-or-history; zle -K main }
function prefix-up()    { zle up-line-or-history;   zle -K main }
function prefix-right() { zle forward-char;   zle -K main }
zle -N prefix-left
zle -N prefix-down
zle -N prefix-up
zle -N prefix-right

# 5) Prefix キーマップへ割り当て
bindkey -M udk_zsh 'h' prefix-left
bindkey -M udk_zsh 'j' prefix-down
bindkey -M udk_zsh 'k' prefix-up
bindkey -M udk_zsh 'l' prefix-right

# Esc でキャンセルして main に戻る
function prefix-cancel() { zle -K main; zle -M "" }
zle -N prefix-cancel
bindkey -M udk_zsh '\e' prefix-cancel

# 6) Prefix キー（例: Ctrl-Space = '^@' が多い）
bindkey '^z' zle-enter-prefix

# fzf-task-command-copy
function tl() {
    local src=$(task | grep -v "Available tasks for" | fzf)
    local selected=$(echo $src | cut -d' ' -f2)
    local selectedCommand=${selected%?}
    echo -n "task $selectedCommand" | pbcopy
    # printf "\n$Grn * SELECTED:$Rst \t$selectedCommand"
    local desc=$(echo $src | awk -F'[:*;]' '{print $4}' | sed 's/^ *//')
    local eg=$(echo $src | awk -F'[;]' '{print $2}' | sed 's/^ *//')
    printf "\n$Grn * DESC:$Rst \t$desc"
    printf "\n$Grn * COPIED AS:$Rst \ttask $selectedCommand"
    printf "\n$Grn * USAGE:$Rst \t$eg"
    zle accept-line
    zle -R -c
    # zle -K main
}
zle -N tl
bindkey -M udk_zsh 't' tl

function open-neovim() {
    nvim
    zle accept-line
}
zle -N open-neovim
bindkey -M udk_zsh 'n' open-neovim

# see: https://zenn.dev/mozumasu/articles/mozumasu-lazy-git
# fzf-ghq
function fzf-ghq() {
  local src=$(ghq list | fzf --preview "bat --color=always --style=header,grid --line-range :80 $(ghq root)/{}/README.*")
  if [ -n "$src" ]; then
    BUFFER="cd $(ghq root)/$src"
    zle accept-line
  fi
  zle -R -c
  zle -K main
}
zle -N fzf-ghq
bindkey -M udk_zsh 'r' fzf-ghq

function fzf-zellij-attach() {
  local session=$(zellij list-sessions --short | fzf)
  if [ -n "$session" ]; then
    BUFFER="zellij attach $session"
    zle accept-line
  fi
  zle -R -c
  zle -K main
}
zle -N fzf-zellij-attach
bindkey -M udk_zsh 'z' fzf-zellij-attach

